-- This is the controller
class Controller

values
    pi = 3.14159265359;
    ctl_torque_max=1/16*pi;
    
instance variables
    io : IO := new IO();
    ctl_angle : real;
    ctl_angle_target : real;

    ctl_torque : real;

    target_is_set : bool;

    moving_state: <Moving> | <Stopped>;

operations
    public Controller : () ==> Controller
    Controller()==(
        ctl_angle := 0;
        ctl_angle_target := 0;
        ctl_torque := 0;

        moving_state := <Stopped>;
        
        target_is_set := true;
    );
    public SetTarget : real ==> ()
    SetTarget(target) == duration(1E6) (
        dcl str : seq of char := "Old target: ";
        target_is_set := false;
        str := str ^ VDMUtil`val2seq_of_char[real](ctl_angle_target);
        str := str ^ " New target: ";
        ctl_angle_target := target;
        str := str ^ VDMUtil`val2seq_of_char[real](ctl_angle_target);
        str := str ^ "\n";
        def - = io.echo(str) in skip;
        target_is_set := true;
    );

    public ReadAngle : () ==> ()
    ReadAngle() == duration(1E7) (
        ctl_angle := ArmSystem`potentio.GetAngle();
    );

    public UpdateArmtorque : () ==> ()
    UpdateArmtorque() == duration(2E6) (
        dcl str : seq of char := "Old torque ctl: ";
        str := str ^ VDMUtil`val2seq_of_char[real](ctl_torque);
        -- find estimated torque
        ctl_torque := (ctl_angle_target - ctl_angle) / 200;

        if(ctl_angle_target - ctl_angle < 0.1 and ctl_angle_target - ctl_angle > -0.1) then
            moving_state := <Stopped>
        else
            moving_state := <Moving>;
        
        -- protect from going over max torque
        if ctl_torque > ctl_torque_max then
            ctl_torque := ctl_torque_max
        elseif ctl_torque < -ctl_torque_max then
            ctl_torque := -ctl_torque_max;

        if(moving_state = <Stopped>) then
            ctl_torque := 0;
    
        -- set the torque in the RobotArm
        ArmSystem`motor.SetTorque(ctl_torque);
        
        str := str ^ " New torque ctl: ";
        str := str ^ VDMUtil`val2seq_of_char[real](ctl_torque);
        str := str ^ "\n";
        def - = io.echo(str) in skip;
    );

sync 
    per SetTarget=> (target_is_set = true);

end Controller