-- This is the controller
class Controller
instance variables
    io : IO := new IO();
    ctl_angle : real;
    ctl_angle_target : real;
    ctl_velocity : real;
    target_is_set : bool;

    moving_state: <Moving> | <Stopped>;

operations
    public Controller : () ==> Controller
    Controller()==(
        ctl_angle := 0;
        ctl_angle_target := 0;
        ctl_velocity := 0;

        moving_state := <Stopped>;
        
        target_is_set := true;
    );
    public SetTarget : real ==> ()
    SetTarget(target) == duration(1E6) (
        dcl str : seq of char := "Old target: ";
        target_is_set := false;
        str := str ^ VDMUtil`val2seq_of_char[real](ctl_angle_target);
        str := str ^ " New target: ";
        ctl_angle_target := target;
        str := str ^ VDMUtil`val2seq_of_char[real](ctl_angle_target);
        str := str ^ "\n";
        def - = io.echo(str) in skip;
        target_is_set := true;
    );

    public GetAngle : () ==> ()
    GetAngle() == duration(1E7) (
        ctl_angle := ArmSystem`roboarm.GetAngle();
    );

    public UpdateArmVelocity : () ==> ()
    UpdateArmVelocity() == duration(2E6) (
        dcl str : seq of char := "Old velocity ctl: ";
        str := str ^ VDMUtil`val2seq_of_char[real](ctl_velocity);
        -- find estimated velocity
        ctl_velocity := (ctl_angle_target - ctl_angle) / 100;

        if(ctl_angle_target - ctl_angle < 0.1 and ctl_angle_target - ctl_angle > -0.1) then
            moving_state := <Stopped>
        else
            moving_state := <Moving>;
        
        -- protect from going over max velocity
        if ctl_velocity > ArmSystem`roboarm.GetMaxVelocity() then
            ctl_velocity := ArmSystem`roboarm.GetMaxVelocity()
        elseif ctl_velocity < -ArmSystem`roboarm.GetMaxVelocity() then
            ctl_velocity := -ArmSystem`roboarm.GetMaxVelocity();

        if(moving_state = <Stopped>) then
            ctl_velocity := 0;
    
        -- set the velocity in the RobotArm
        ArmSystem`roboarm.SetVelocity(ctl_velocity);
        
        str := str ^ " New velocity ctl: ";
        str := str ^ VDMUtil`val2seq_of_char[real](ctl_velocity);
        str := str ^ "\n";
        def - = io.echo(str) in skip;
    );

sync 
    per SetTarget=> (target_is_set = true);

end Controller