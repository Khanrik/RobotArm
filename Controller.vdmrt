-- This is the controller
class Controller

values
    pi = 3.14159265359;
    ctl_torque_max=1/16*pi;
    
instance variables
    io : IO := new IO();
    ctl_angle : real;
    ctl_angle_target : real;

    ctl_torque : real;

    target_is_set : bool;

    moving_state: <Moving> | <Stopped>;

    controller_state: <Init> | <Normal> | <Stop>;

operations
    public Controller : () ==> Controller
    Controller()==(
        ctl_angle := 0;
        ctl_angle_target := 0;
        ctl_torque := 0;

        moving_state := <Stopped>;

        controller_state := <Init>;
        
        target_is_set := true;
    );
    public SetTarget : real ==> ()
    SetTarget(target) == duration(1E6) (
        dcl str : seq of char := "Old target: ";
        target_is_set := false;
        str := str ^ VDMUtil`val2seq_of_char[real](ctl_angle_target);
        str := str ^ " New target: ";

        if not (controller_state = <Normal>) then (
            ctl_angle_target := target
        );

        str := str ^ VDMUtil`val2seq_of_char[real](ctl_angle_target);
        str := str ^ "\n";
        def - = io.echo(str) in skip;
        target_is_set := true;
    );

    public ReadAngle : () ==> ()
    ReadAngle() == duration(1E7) (
        dcl new_angle : real := ArmSystem`potentio.GetAngle();
        if(abs(new_angle - ctl_angle) > 0.5) then ( -- Stop if the current angle is too far from the target
            controller_state := <Stop>;

            def - = io.echo("Emergency stop!\n") in skip;
            );
        ctl_angle := new_angle;
    );

    public UpdateArmtorque : () ==> ()
    UpdateArmtorque() == duration(2E6) (
        dcl str : seq of char := "Old torque ctl: ";
        str := str ^ VDMUtil`val2seq_of_char[real](ctl_torque);

        if(controller_state = <Stop>) then (
            ctl_torque := 0;
            ArmSystem`motor.SetTorque(ctl_torque);

            str := str ^ " New torque ctl: ";
            str := str ^ VDMUtil`val2seq_of_char[real](ctl_torque);
            str := str ^ "\n";
            def - = io.echo(str) in skip;

            return;
        );

        -- find estimated torque
        if(moving_state = <Moving>) then
            ctl_torque := (ctl_angle_target - ctl_angle)/100;

        if(ctl_angle_target - ctl_angle < 0.01 and ctl_angle_target - ctl_angle > -0.01) then
            moving_state := <Stopped>
        else
            moving_state := <Moving>;
        
        -- protect from going over max torque
        if ctl_torque > ctl_torque_max then
            ctl_torque := ctl_torque_max
        elseif ctl_torque < -ctl_torque_max then
            ctl_torque := -ctl_torque_max;

        if(moving_state = <Stopped>) then
            ctl_torque := 0;
    
        -- set the torque in the RobotArm
        ArmSystem`motor.SetTorque(ctl_torque);
        
        str := str ^ " New torque ctl: ";
        str := str ^ VDMUtil`val2seq_of_char[real](ctl_torque);
        str := str ^ "\n";
        def - = io.echo(str) in skip;
    );

    public Init : () ==> ()
    Init() == duration(1E6) (
        def - = io.echo("Controller Init\n") in skip;
        while ctl_angle > 0.001 
        do (
            UpdateArmtorque();  
            ReadAngle();
            ArmSystem`roboarm.Step();
        );
    );

sync 
    per SetTarget=> (target_is_set = true);

end Controller